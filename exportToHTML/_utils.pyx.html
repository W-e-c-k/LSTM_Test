<html>
<head>
<title>_utils.pyx</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<style type="text/css">
.s0 { color: #bcbec4;}
</style>
</head>
<body bgcolor="#1e1f22">
<table CELLSPACING=0 CELLPADDING=5 COLS=1 WIDTH="100%" BGCOLOR="#606060" >
<tr><td><center>
<font face="Arial, Helvetica" color="#000000">
_utils.pyx</font>
</center></td></tr></table>
<pre><span class="s0"># Authors: Gilles Louppe &lt;g.louppe@gmail.com&gt;</span>
<span class="s0">#          Peter Prettenhofer &lt;peter.prettenhofer@gmail.com&gt;</span>
<span class="s0">#          Arnaud Joly &lt;arnaud.v.joly@gmail.com&gt;</span>
<span class="s0">#          Jacob Schreiber &lt;jmschreiber91@gmail.com&gt;</span>
<span class="s0">#          Nelson Liu &lt;nelson@nelsonliu.me&gt;</span>
<span class="s0">#</span>
<span class="s0">#</span>
<span class="s0"># License: BSD 3 clause</span>

<span class="s0">from libc.stdlib cimport free</span>
<span class="s0">from libc.stdlib cimport realloc</span>
<span class="s0">from libc.math cimport log as ln</span>
<span class="s0">from libc.math cimport isnan</span>

<span class="s0">import numpy as np</span>
<span class="s0">cimport numpy as cnp</span>
<span class="s0">cnp.import_array()</span>

<span class="s0">from ..utils._random cimport our_rand_r</span>

<span class="s0"># =============================================================================</span>
<span class="s0"># Helper functions</span>
<span class="s0"># =============================================================================</span>

<span class="s0">cdef int safe_realloc(realloc_ptr* p, size_t nelems) except -1 nogil:</span>
    <span class="s0"># sizeof(realloc_ptr[0]) would be more like idiomatic C, but causes Cython</span>
    <span class="s0"># 0.20.1 to crash.</span>
    <span class="s0">cdef size_t nbytes = nelems * sizeof(p[0][0])</span>
    <span class="s0">if nbytes / sizeof(p[0][0]) != nelems:</span>
        <span class="s0"># Overflow in the multiplication</span>
        <span class="s0">raise MemoryError(f&quot;could not allocate ({nelems} * {sizeof(p[0][0])}) bytes&quot;)</span>

    <span class="s0">cdef realloc_ptr tmp = &lt;realloc_ptr&gt;realloc(p[0], nbytes)</span>
    <span class="s0">if tmp == NULL:</span>
        <span class="s0">raise MemoryError(f&quot;could not allocate {nbytes} bytes&quot;)</span>

    <span class="s0">p[0] = tmp</span>
    <span class="s0">return 0</span>


<span class="s0">def _realloc_test():</span>
    <span class="s0"># Helper for tests. Tries to allocate &lt;size_t&gt;(-1) / 2 * sizeof(size_t)</span>
    <span class="s0"># bytes, which will always overflow.</span>
    <span class="s0">cdef intp_t* p = NULL</span>
    <span class="s0">safe_realloc(&amp;p, &lt;size_t&gt;(-1) / 2)</span>
    <span class="s0">if p != NULL:</span>
        <span class="s0">free(p)</span>
        <span class="s0">assert False</span>


<span class="s0">cdef inline cnp.ndarray sizet_ptr_to_ndarray(intp_t* data, intp_t size):</span>
    <span class="s0">&quot;&quot;&quot;Return copied data as 1D numpy array of intp's.&quot;&quot;&quot;</span>
    <span class="s0">cdef cnp.npy_intp shape[1]</span>
    <span class="s0">shape[0] = &lt;cnp.npy_intp&gt; size</span>
    <span class="s0">return cnp.PyArray_SimpleNewFromData(1, shape, cnp.NPY_INTP, data).copy()</span>


<span class="s0">cdef inline intp_t rand_int(intp_t low, intp_t high,</span>
                            <span class="s0">uint32_t* random_state) noexcept nogil:</span>
    <span class="s0">&quot;&quot;&quot;Generate a random integer in [low; end).&quot;&quot;&quot;</span>
    <span class="s0">return low + our_rand_r(random_state) % (high - low)</span>


<span class="s0">cdef inline float64_t rand_uniform(float64_t low, float64_t high,</span>
                                   <span class="s0">uint32_t* random_state) noexcept nogil:</span>
    <span class="s0">&quot;&quot;&quot;Generate a random float64_t in [low; high).&quot;&quot;&quot;</span>
    <span class="s0">return ((high - low) * &lt;float64_t&gt; our_rand_r(random_state) /</span>
            <span class="s0">&lt;float64_t&gt; RAND_R_MAX) + low</span>


<span class="s0">cdef inline float64_t log(float64_t x) noexcept nogil:</span>
    <span class="s0">return ln(x) / ln(2.0)</span>

<span class="s0"># =============================================================================</span>
<span class="s0"># WeightedPQueue data structure</span>
<span class="s0"># =============================================================================</span>

<span class="s0">cdef class WeightedPQueue:</span>
    <span class="s0">&quot;&quot;&quot;A priority queue class, always sorted in increasing order.</span>

    <span class="s0">Attributes</span>
    <span class="s0">----------</span>
    <span class="s0">capacity : intp_t</span>
        <span class="s0">The capacity of the priority queue.</span>

    <span class="s0">array_ptr : intp_t</span>
        <span class="s0">The water mark of the priority queue; the priority queue grows from</span>
        <span class="s0">left to right in the array ``array_``. ``array_ptr`` is always</span>
        <span class="s0">less than ``capacity``.</span>

    <span class="s0">array_ : WeightedPQueueRecord*</span>
        <span class="s0">The array of priority queue records. The minimum element is on the</span>
        <span class="s0">left at index 0, and the maximum element is on the right at index</span>
        <span class="s0">``array_ptr-1``.</span>
    <span class="s0">&quot;&quot;&quot;</span>

    <span class="s0">def __cinit__(self, intp_t capacity):</span>
        <span class="s0">self.capacity = capacity</span>
        <span class="s0">self.array_ptr = 0</span>
        <span class="s0">safe_realloc(&amp;self.array_, capacity)</span>

    <span class="s0">def __dealloc__(self):</span>
        <span class="s0">free(self.array_)</span>

    <span class="s0">cdef int reset(self) except -1 nogil:</span>
        <span class="s0">&quot;&quot;&quot;Reset the WeightedPQueue to its state at construction</span>

        <span class="s0">Return -1 in case of failure to allocate memory (and raise MemoryError)</span>
        <span class="s0">or 0 otherwise.</span>
        <span class="s0">&quot;&quot;&quot;</span>
        <span class="s0">self.array_ptr = 0</span>
        <span class="s0"># Since safe_realloc can raise MemoryError, use `except -1`</span>
        <span class="s0">safe_realloc(&amp;self.array_, self.capacity)</span>
        <span class="s0">return 0</span>

    <span class="s0">cdef bint is_empty(self) noexcept nogil:</span>
        <span class="s0">return self.array_ptr &lt;= 0</span>

    <span class="s0">cdef intp_t size(self) noexcept nogil:</span>
        <span class="s0">return self.array_ptr</span>

    <span class="s0">cdef int push(self, float64_t data, float64_t weight) except -1 nogil:</span>
        <span class="s0">&quot;&quot;&quot;Push record on the array.</span>

        <span class="s0">Return -1 in case of failure to allocate memory (and raise MemoryError)</span>
        <span class="s0">or 0 otherwise.</span>
        <span class="s0">&quot;&quot;&quot;</span>
        <span class="s0">cdef intp_t array_ptr = self.array_ptr</span>
        <span class="s0">cdef WeightedPQueueRecord* array = NULL</span>
        <span class="s0">cdef intp_t i</span>

        <span class="s0"># Resize if capacity not sufficient</span>
        <span class="s0">if array_ptr &gt;= self.capacity:</span>
            <span class="s0">self.capacity *= 2</span>
            <span class="s0"># Since safe_realloc can raise MemoryError, use `except -1`</span>
            <span class="s0">safe_realloc(&amp;self.array_, self.capacity)</span>

        <span class="s0"># Put element as last element of array</span>
        <span class="s0">array = self.array_</span>
        <span class="s0">array[array_ptr].data = data</span>
        <span class="s0">array[array_ptr].weight = weight</span>

        <span class="s0"># bubble last element up according until it is sorted</span>
        <span class="s0"># in ascending order</span>
        <span class="s0">i = array_ptr</span>
        <span class="s0">while(i != 0 and array[i].data &lt; array[i-1].data):</span>
            <span class="s0">array[i], array[i-1] = array[i-1], array[i]</span>
            <span class="s0">i -= 1</span>

        <span class="s0"># Increase element count</span>
        <span class="s0">self.array_ptr = array_ptr + 1</span>
        <span class="s0">return 0</span>

    <span class="s0">cdef int remove(self, float64_t data, float64_t weight) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Remove a specific value/weight record from the array.</span>
        <span class="s0">Returns 0 if successful, -1 if record not found.&quot;&quot;&quot;</span>
        <span class="s0">cdef intp_t array_ptr = self.array_ptr</span>
        <span class="s0">cdef WeightedPQueueRecord* array = self.array_</span>
        <span class="s0">cdef intp_t idx_to_remove = -1</span>
        <span class="s0">cdef intp_t i</span>

        <span class="s0">if array_ptr &lt;= 0:</span>
            <span class="s0">return -1</span>

        <span class="s0"># find element to remove</span>
        <span class="s0">for i in range(array_ptr):</span>
            <span class="s0">if array[i].data == data and array[i].weight == weight:</span>
                <span class="s0">idx_to_remove = i</span>
                <span class="s0">break</span>

        <span class="s0">if idx_to_remove == -1:</span>
            <span class="s0">return -1</span>

        <span class="s0"># shift the elements after the removed element</span>
        <span class="s0"># to the left.</span>
        <span class="s0">for i in range(idx_to_remove, array_ptr-1):</span>
            <span class="s0">array[i] = array[i+1]</span>

        <span class="s0">self.array_ptr = array_ptr - 1</span>
        <span class="s0">return 0</span>

    <span class="s0">cdef int pop(self, float64_t* data, float64_t* weight) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Remove the top (minimum) element from array.</span>
        <span class="s0">Returns 0 if successful, -1 if nothing to remove.&quot;&quot;&quot;</span>
        <span class="s0">cdef intp_t array_ptr = self.array_ptr</span>
        <span class="s0">cdef WeightedPQueueRecord* array = self.array_</span>
        <span class="s0">cdef intp_t i</span>

        <span class="s0">if array_ptr &lt;= 0:</span>
            <span class="s0">return -1</span>

        <span class="s0">data[0] = array[0].data</span>
        <span class="s0">weight[0] = array[0].weight</span>

        <span class="s0"># shift the elements after the removed element</span>
        <span class="s0"># to the left.</span>
        <span class="s0">for i in range(0, array_ptr-1):</span>
            <span class="s0">array[i] = array[i+1]</span>

        <span class="s0">self.array_ptr = array_ptr - 1</span>
        <span class="s0">return 0</span>

    <span class="s0">cdef int peek(self, float64_t* data, float64_t* weight) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Write the top element from array to a pointer.</span>
        <span class="s0">Returns 0 if successful, -1 if nothing to write.&quot;&quot;&quot;</span>
        <span class="s0">cdef WeightedPQueueRecord* array = self.array_</span>
        <span class="s0">if self.array_ptr &lt;= 0:</span>
            <span class="s0">return -1</span>
        <span class="s0"># Take first value</span>
        <span class="s0">data[0] = array[0].data</span>
        <span class="s0">weight[0] = array[0].weight</span>
        <span class="s0">return 0</span>

    <span class="s0">cdef float64_t get_weight_from_index(self, intp_t index) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Given an index between [0,self.current_capacity], access</span>
        <span class="s0">the appropriate heap and return the requested weight&quot;&quot;&quot;</span>
        <span class="s0">cdef WeightedPQueueRecord* array = self.array_</span>

        <span class="s0"># get weight at index</span>
        <span class="s0">return array[index].weight</span>

    <span class="s0">cdef float64_t get_value_from_index(self, intp_t index) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Given an index between [0,self.current_capacity], access</span>
        <span class="s0">the appropriate heap and return the requested value&quot;&quot;&quot;</span>
        <span class="s0">cdef WeightedPQueueRecord* array = self.array_</span>

        <span class="s0"># get value at index</span>
        <span class="s0">return array[index].data</span>

<span class="s0"># =============================================================================</span>
<span class="s0"># WeightedMedianCalculator data structure</span>
<span class="s0"># =============================================================================</span>

<span class="s0">cdef class WeightedMedianCalculator:</span>
    <span class="s0">&quot;&quot;&quot;A class to handle calculation of the weighted median from streams of</span>
    <span class="s0">data. To do so, it maintains a parameter ``k`` such that the sum of the</span>
    <span class="s0">weights in the range [0,k) is greater than or equal to half of the total</span>
    <span class="s0">weight. By minimizing the value of ``k`` that fulfills this constraint,</span>
    <span class="s0">calculating the median is done by either taking the value of the sample</span>
    <span class="s0">at index ``k-1`` of ``samples`` (samples[k-1].data) or the average of</span>
    <span class="s0">the samples at index ``k-1`` and ``k`` of ``samples``</span>
    <span class="s0">((samples[k-1] + samples[k]) / 2).</span>

    <span class="s0">Attributes</span>
    <span class="s0">----------</span>
    <span class="s0">initial_capacity : intp_t</span>
        <span class="s0">The initial capacity of the WeightedMedianCalculator.</span>

    <span class="s0">samples : WeightedPQueue</span>
        <span class="s0">Holds the samples (consisting of values and their weights) used in the</span>
        <span class="s0">weighted median calculation.</span>

    <span class="s0">total_weight : float64_t</span>
        <span class="s0">The sum of the weights of items in ``samples``. Represents the total</span>
        <span class="s0">weight of all samples used in the median calculation.</span>

    <span class="s0">k : intp_t</span>
        <span class="s0">Index used to calculate the median.</span>

    <span class="s0">sum_w_0_k : float64_t</span>
        <span class="s0">The sum of the weights from samples[0:k]. Used in the weighted</span>
        <span class="s0">median calculation; minimizing the value of ``k`` such that</span>
        <span class="s0">``sum_w_0_k`` &gt;= ``total_weight / 2`` provides a mechanism for</span>
        <span class="s0">calculating the median in constant time.</span>

    <span class="s0">&quot;&quot;&quot;</span>

    <span class="s0">def __cinit__(self, intp_t initial_capacity):</span>
        <span class="s0">self.initial_capacity = initial_capacity</span>
        <span class="s0">self.samples = WeightedPQueue(initial_capacity)</span>
        <span class="s0">self.total_weight = 0</span>
        <span class="s0">self.k = 0</span>
        <span class="s0">self.sum_w_0_k = 0</span>

    <span class="s0">cdef intp_t size(self) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Return the number of samples in the</span>
        <span class="s0">WeightedMedianCalculator&quot;&quot;&quot;</span>
        <span class="s0">return self.samples.size()</span>

    <span class="s0">cdef int reset(self) except -1 nogil:</span>
        <span class="s0">&quot;&quot;&quot;Reset the WeightedMedianCalculator to its state at construction</span>

        <span class="s0">Return -1 in case of failure to allocate memory (and raise MemoryError)</span>
        <span class="s0">or 0 otherwise.</span>
        <span class="s0">&quot;&quot;&quot;</span>
        <span class="s0"># samples.reset (WeightedPQueue.reset) uses safe_realloc, hence</span>
        <span class="s0"># except -1</span>
        <span class="s0">self.samples.reset()</span>
        <span class="s0">self.total_weight = 0</span>
        <span class="s0">self.k = 0</span>
        <span class="s0">self.sum_w_0_k = 0</span>
        <span class="s0">return 0</span>

    <span class="s0">cdef int push(self, float64_t data, float64_t weight) except -1 nogil:</span>
        <span class="s0">&quot;&quot;&quot;Push a value and its associated weight to the WeightedMedianCalculator</span>

        <span class="s0">Return -1 in case of failure to allocate memory (and raise MemoryError)</span>
        <span class="s0">or 0 otherwise.</span>
        <span class="s0">&quot;&quot;&quot;</span>
        <span class="s0">cdef int return_value</span>
        <span class="s0">cdef float64_t original_median = 0.0</span>

        <span class="s0">if self.size() != 0:</span>
            <span class="s0">original_median = self.get_median()</span>
        <span class="s0"># samples.push (WeightedPQueue.push) uses safe_realloc, hence except -1</span>
        <span class="s0">return_value = self.samples.push(data, weight)</span>
        <span class="s0">self.update_median_parameters_post_push(data, weight,</span>
                                                <span class="s0">original_median)</span>
        <span class="s0">return return_value</span>

    <span class="s0">cdef int update_median_parameters_post_push(</span>
            <span class="s0">self, float64_t data, float64_t weight,</span>
            <span class="s0">float64_t original_median) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Update the parameters used in the median calculation,</span>
        <span class="s0">namely `k` and `sum_w_0_k` after an insertion&quot;&quot;&quot;</span>

        <span class="s0"># trivial case of one element.</span>
        <span class="s0">if self.size() == 1:</span>
            <span class="s0">self.k = 1</span>
            <span class="s0">self.total_weight = weight</span>
            <span class="s0">self.sum_w_0_k = self.total_weight</span>
            <span class="s0">return 0</span>

        <span class="s0"># get the original weighted median</span>
        <span class="s0">self.total_weight += weight</span>

        <span class="s0">if data &lt; original_median:</span>
            <span class="s0"># inserting below the median, so increment k and</span>
            <span class="s0"># then update self.sum_w_0_k accordingly by adding</span>
            <span class="s0"># the weight that was added.</span>
            <span class="s0">self.k += 1</span>
            <span class="s0"># update sum_w_0_k by adding the weight added</span>
            <span class="s0">self.sum_w_0_k += weight</span>

            <span class="s0"># minimize k such that sum(W[0:k]) &gt;= total_weight / 2</span>
            <span class="s0"># minimum value of k is 1</span>
            <span class="s0">while(self.k &gt; 1 and ((self.sum_w_0_k -</span>
                                   <span class="s0">self.samples.get_weight_from_index(self.k-1))</span>
                                  <span class="s0">&gt;= self.total_weight / 2.0)):</span>
                <span class="s0">self.k -= 1</span>
                <span class="s0">self.sum_w_0_k -= self.samples.get_weight_from_index(self.k)</span>
            <span class="s0">return 0</span>

        <span class="s0">if data &gt;= original_median:</span>
            <span class="s0"># inserting above or at the median</span>
            <span class="s0"># minimize k such that sum(W[0:k]) &gt;= total_weight / 2</span>
            <span class="s0">while(self.k &lt; self.samples.size() and</span>
                  <span class="s0">(self.sum_w_0_k &lt; self.total_weight / 2.0)):</span>
                <span class="s0">self.k += 1</span>
                <span class="s0">self.sum_w_0_k += self.samples.get_weight_from_index(self.k-1)</span>
            <span class="s0">return 0</span>

    <span class="s0">cdef int remove(self, float64_t data, float64_t weight) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Remove a value from the MedianHeap, removing it</span>
        <span class="s0">from consideration in the median calculation</span>
        <span class="s0">&quot;&quot;&quot;</span>
        <span class="s0">cdef int return_value</span>
        <span class="s0">cdef float64_t original_median = 0.0</span>

        <span class="s0">if self.size() != 0:</span>
            <span class="s0">original_median = self.get_median()</span>

        <span class="s0">return_value = self.samples.remove(data, weight)</span>
        <span class="s0">self.update_median_parameters_post_remove(data, weight,</span>
                                                  <span class="s0">original_median)</span>
        <span class="s0">return return_value</span>

    <span class="s0">cdef int pop(self, float64_t* data, float64_t* weight) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Pop a value from the MedianHeap, starting from the</span>
        <span class="s0">left and moving to the right.</span>
        <span class="s0">&quot;&quot;&quot;</span>
        <span class="s0">cdef int return_value</span>
        <span class="s0">cdef float64_t original_median = 0.0</span>

        <span class="s0">if self.size() != 0:</span>
            <span class="s0">original_median = self.get_median()</span>

        <span class="s0"># no elements to pop</span>
        <span class="s0">if self.samples.size() == 0:</span>
            <span class="s0">return -1</span>

        <span class="s0">return_value = self.samples.pop(data, weight)</span>
        <span class="s0">self.update_median_parameters_post_remove(data[0],</span>
                                                  <span class="s0">weight[0],</span>
                                                  <span class="s0">original_median)</span>
        <span class="s0">return return_value</span>

    <span class="s0">cdef int update_median_parameters_post_remove(</span>
            <span class="s0">self, float64_t data, float64_t weight,</span>
            <span class="s0">float64_t original_median) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Update the parameters used in the median calculation,</span>
        <span class="s0">namely `k` and `sum_w_0_k` after a removal&quot;&quot;&quot;</span>
        <span class="s0"># reset parameters because it there are no elements</span>
        <span class="s0">if self.samples.size() == 0:</span>
            <span class="s0">self.k = 0</span>
            <span class="s0">self.total_weight = 0</span>
            <span class="s0">self.sum_w_0_k = 0</span>
            <span class="s0">return 0</span>

        <span class="s0"># trivial case of one element.</span>
        <span class="s0">if self.samples.size() == 1:</span>
            <span class="s0">self.k = 1</span>
            <span class="s0">self.total_weight -= weight</span>
            <span class="s0">self.sum_w_0_k = self.total_weight</span>
            <span class="s0">return 0</span>

        <span class="s0"># get the current weighted median</span>
        <span class="s0">self.total_weight -= weight</span>

        <span class="s0">if data &lt; original_median:</span>
            <span class="s0"># removing below the median, so decrement k and</span>
            <span class="s0"># then update self.sum_w_0_k accordingly by subtracting</span>
            <span class="s0"># the removed weight</span>

            <span class="s0">self.k -= 1</span>
            <span class="s0"># update sum_w_0_k by removing the weight at index k</span>
            <span class="s0">self.sum_w_0_k -= weight</span>

            <span class="s0"># minimize k such that sum(W[0:k]) &gt;= total_weight / 2</span>
            <span class="s0"># by incrementing k and updating sum_w_0_k accordingly</span>
            <span class="s0"># until the condition is met.</span>
            <span class="s0">while(self.k &lt; self.samples.size() and</span>
                  <span class="s0">(self.sum_w_0_k &lt; self.total_weight / 2.0)):</span>
                <span class="s0">self.k += 1</span>
                <span class="s0">self.sum_w_0_k += self.samples.get_weight_from_index(self.k-1)</span>
            <span class="s0">return 0</span>

        <span class="s0">if data &gt;= original_median:</span>
            <span class="s0"># removing above the median</span>
            <span class="s0"># minimize k such that sum(W[0:k]) &gt;= total_weight / 2</span>
            <span class="s0">while(self.k &gt; 1 and ((self.sum_w_0_k -</span>
                                   <span class="s0">self.samples.get_weight_from_index(self.k-1))</span>
                                  <span class="s0">&gt;= self.total_weight / 2.0)):</span>
                <span class="s0">self.k -= 1</span>
                <span class="s0">self.sum_w_0_k -= self.samples.get_weight_from_index(self.k)</span>
            <span class="s0">return 0</span>

    <span class="s0">cdef float64_t get_median(self) noexcept nogil:</span>
        <span class="s0">&quot;&quot;&quot;Write the median to a pointer, taking into account</span>
        <span class="s0">sample weights.&quot;&quot;&quot;</span>
        <span class="s0">if self.sum_w_0_k == (self.total_weight / 2.0):</span>
            <span class="s0"># split median</span>
            <span class="s0">return (self.samples.get_value_from_index(self.k) +</span>
                    <span class="s0">self.samples.get_value_from_index(self.k-1)) / 2.0</span>
        <span class="s0">if self.sum_w_0_k &gt; (self.total_weight / 2.0):</span>
            <span class="s0"># whole median</span>
            <span class="s0">return self.samples.get_value_from_index(self.k-1)</span>


<span class="s0">def _any_isnan_axis0(const float32_t[:, :] X):</span>
    <span class="s0">&quot;&quot;&quot;Same as np.any(np.isnan(X), axis=0)&quot;&quot;&quot;</span>
    <span class="s0">cdef:</span>
        <span class="s0">intp_t i, j</span>
        <span class="s0">intp_t n_samples = X.shape[0]</span>
        <span class="s0">intp_t n_features = X.shape[1]</span>
        <span class="s0">unsigned char[::1] isnan_out = np.zeros(X.shape[1], dtype=np.bool_)</span>

    <span class="s0">with nogil:</span>
        <span class="s0">for i in range(n_samples):</span>
            <span class="s0">for j in range(n_features):</span>
                <span class="s0">if isnan_out[j]:</span>
                    <span class="s0">continue</span>
                <span class="s0">if isnan(X[i, j]):</span>
                    <span class="s0">isnan_out[j] = True</span>
                    <span class="s0">break</span>
    <span class="s0">return np.asarray(isnan_out)</span>
</pre>
</body>
</html>